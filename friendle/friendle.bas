!--------------------------------------------------
!- Wednesday, March 6, 2024 4:47:09 PM
!- Import of : 
!- c:\dev\c-ordle\friendle\vicdle.prg
!- Commodore 64
!--------------------------------------------------
10 REM WORDLE - FOR VIC
20 REM ======
30 REM BY BO ZIMMERMAN AND STEVE GRAY
40 REM V1.0, 2024-03-6
50 REM-------------------------------
60 :
70 PRINTCHR$(142);:IFPEEK(56)<>39THENPOKE56,39:POKE55,16:CLR
80 :
90 MW=12943:POKE53280,0:POKE53281,0:PRINT"{white}";
100 W$=""
110 T$=" "
120 Z$="{reverse on}{space*2}{reverse off}{white}="
130 B1$="{reverse on}O{cm y}P{down}{left*3}{cm g}"
140 B2$="{cm n}{down}{left*3}L{cm p}{sh @}{up*2}"
150 P$="{home}{down*21}"
160 L$="{white}CCCCCCCCCCCCCCCCCCCCCC"
170 IFPEEK(6064)=76 THEN 210
180 IFL=0THENGOSUB2000
190 IFL=0THENL=1:LOAD"friendict2.bin",8,1
200 IFL=1THENL=2:LOAD"friendlml.bin",8,1
205 IFL=2THENL=3:LOAD"friendict.bin",8,1
210 GOSUB3000
220 REM---- GENERATE THE WORD
230 :
240 WX=INT(RND(.)*MW)
250 POKE10007,INT(WX/256)
260 POKE10006,WX-(256*PEEK(10007))
270 SYS10000:IFPEEK(10006)=0THEN240
280 W$=""
290 FORI=0TO4
300 W$=W$+CHR$(PEEK(10006+I))
310 NEXT
315 SC(7)=SC(7)+1
320 :
330 REM---- DRAW SCREEN
340 :
350 PRINT"{clear}{white}friendle-guess the word!"
360 PRINT L$
370 S$="{space*5}"
380 N=3:GOSUB1270
390 FORJ=1TO6
400 PRINT T$;
410 GOSUB940
420 NEXT J
430 PRINT"{down}";L$
450 N=0:GOSUB1270:PRINT Z$;"correct ";
460 N=2:GOSUB1270:PRINT Z$;"place "
470 N=1:GOSUB1270:PRINT Z$;"wrong";
480 PRINT"{home}{down}
490 :
500 REM---- ENTER A GUESS
510 :
520 GU=1
530 PRINT T$;
540 C=0:G$=""
550 E=0:GOSUB1070:EN=0
560 :
570 REM---- CHECK IT
580 :
590 FORI=1TO5
600 A$=MID$(G$,I,1)
610 POKE10005+I,ASC(A$)
620 IFMID$(W$,I,1)=A$ THEN FE=0:GOTO670
630 FE=1
640 FOR II=1 TO 5
650 IF MID$(W$,II,1)=A$ THEN IFMID$(G$,II,1) <> A$ THEN FE=2
660 NEXT II
670 AN(I)=FE
680 NEXT I
690 IF E=1 GOTO 550
700 SYS10003
710 IF PEEK(10006)=0 THEN PRINT"{red}{down}not a word!{left*11}{up}";:EN=1:GOTO550
720 :
730 REM---- DISPLAY RESULTS
740 :
750 PRINT"{up}"
760 PRINT T$;
770 FOR I=1 TO 5
780 N=AN(I):GOSUB 1270:C$=MID$(G$,I,1)
790 GOSUB1020
800 NEXT I
810 PRINT"{down*2}
820 :
830 REM---- CHECK FOR WIN
840 :
850 GU=GU+1
860 IF W$=G$ THEN PRINT P$;"{green}you win!":SC(GU-1)=SC(GU-1)+1:GOTO 890
870 IF GU>6 THEN PRINT P$;"{red}you failed.":PRINT"the word was: {white}";W$:GOTO 890
880 GOTO530
890 PRINT"{white}play again (y/n)?{space*4}"
900 GET A$:IF A$="y" GOTO 220
910 IF A$<>"n"GOTO900
912 PRINT"{down*2}thanks for playing!"
915 OPEN8,8,8,"@0:scoredle,s,w":FORI=1TO7:PRINT#8,SC(I):NEXTI:CLOSE8
920 PRINT"{clear}{white}";CHR$(14);:GOSUB3400:END
930 :
940 REM---- PRINT STRING INSIDE BOXES
950 :
960 FORA=1TO LEN(S$)
970 C$=MID$(S$,A,1):GOSUB 1020
980 NEXT
990 PRINT"{down*2}
1000 RETURN
1010 :
1020 REM---- DRAW BOX WITH LETTER INSIDE
1030 :
1040 PRINT B1$;C$;B2$;
1050 RETURN
1060 :
1070 REM---- ENTER GUESS
1080 :
1090 PRINT"{white}";
1100 GET C$:IF C$="" GOTO 1100
1110 IF C=5 AND C$=CHR$(13) THEN RETURN
1120 IFEN=1THENEN=0:PRINT"{down}{white}{reverse off}{space*11}{left*11}{up}";
1130 IF C>0 AND C$=CHR$(20) THEN GOSUB1200
1140 IF C=5 GOTO 1100
1150 IF C$<"a" OR C$>"z" GOTO 1100
1160 GOSUB 1020
1170 G$=G$+C$:C=C+1
1180 GOTO 1100
1190 :
1200 REM---- REMOVE CHARACTER
1210 :
1220 G$=LEFT$(G$,LEN(G$)-1):C=C-1
1230 N=3:GOSUB 1270
1240 PRINT"{left*3}";B1$;" ";B2$;"{left*3}{white}";
1250 RETURN
1260 :
1270 REM---- SET COLOR
1280 :
1290 IF N=0 THEN PRINT"{green}";:REM GREEN
1300 IF N=1 THEN PRINT"{gray}";:REM GREY
1310 IF N=2 THEN PRINT"{yellow}";:REM YELLOW
1320 IF N=3 THEN PRINT"{light gray}";:REM LT GRY
1330 RETURN
2000 REM
2010 :
2015 X$="{space*2}"
2020 PRINT"{clear}{down*3}
2025 N=0:GOSUB 1290:PRINTX$;
2030 S$="{space*2}friendle{space*2}":GOSUB 960
2035 N=1:GOSUB 1290:PRINTX$;
2040 S$="{space*5}by{space*5}":GOSUB 960
2045 N=2:GOSUB 1290:PRINTX$;
2050 S$="bo zimmerman":GOSUB 960
2055 N=1:GOSUB 1290:PRINTX$;
2060 S$="{space*5}and{space*4}":GOSUB 960
2065 N=2:GOSUB 1290:PRINTX$;
2070 S$=" steve gray ":GOSUB 960
2080 PRINT"{white}{down*2}{space*14}loading... "
2100 RETURN
2110 RETURN
3000 DIMSC(7):OPEN1,8,15:OPEN8,8,8,"scoredle,s,r"
3010 INPUT#1,E:IFE>0THEN3030
3020 FORI=1TO7:INPUT#8,SC(I):NEXTI
3030 CLOSE8:CLOSE1
3400 PRINT"totals out of";SC(7);"games."
3410 FORI=1TO6:PRINT"guess";I;":";SC(I):NEXTI:RETURN
